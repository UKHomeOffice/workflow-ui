plugins {
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'groovy'
    id 'jacoco'
}

group = 'io.digitalpatterns'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
    querydsl {
        extendsFrom compileClasspath
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://jitpack.io"
    }
}

test {
    testLogging {
        events "PASSED", "STARTED", "FAILED"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/exception/**',
                              '**/model/**',
                    ]
            )
        }))
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.springframework.cloud:spring-cloud-context:2.2.2.RELEASE'
    implementation 'org.springframework.cloud:spring-cloud-starter-aws-secrets-manager-config:2.2.1.RELEASE'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-zuul:2.2.2.RELEASE'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'net.logstash.logback:logstash-logback-encoder:6.3'

    implementation 'org.springframework.security:spring-security-config:5.2.2.RELEASE'
    implementation 'org.springframework.security:spring-security-oauth2-jose:5.2.2.RELEASE'
    implementation 'org.springframework.security:spring-security-oauth2-resource-server:5.2.2.RELEASE'
    implementation 'org.json:json:20190722'
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'



    compileOnly 'org.projectlombok:lombok'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    annotationProcessor(
            'org.springframework.boot:spring-boot-configuration-processor',
            'org.projectlombok:lombok'
    )
    testImplementation 'org.springframework.security:spring-security-test:5.3.0.RELEASE'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner:2.2.2.RELEASE'

    testImplementation 'org.codehaus.groovy:groovy-all:2.5.10'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'org.spockframework:spock-spring:1.3-groovy-2.5'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

jacoco {
    toolVersion = "0.8.5"
}

bootJar {
    archiveName = "workflow-ui.jar"
    mainClassName = 'io.digitalpatterns.workflow.ui.UIApplication'

    from('../client/build') {
        into 'static'
    }
}

jar.dependsOn(':client:assemble')

